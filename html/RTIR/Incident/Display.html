%# BEGIN LICENSE BLOCK
%# 
%# Copyright (c) 1996-2002 Jesse Vincent <jesse@bestpractical.com>
%# 
%# (Except where explictly superceded by other copyright notices)
%# 
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org
%# 
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%# 
%# 
%# Unless otherwise specified, all modifications, corrections or
%# extensions to this work which alter its source code become the
%# property of Best Practical Solutions, LLC when submitted for
%# inclusion in the work.
%# 
%# 
%# END LICENSE BLOCK

<& /RTIR/Elements/Header, Title => loc("Incident #[_1]: [_2]", $id, $TicketObj->Subject) &>
<& /RTIR/Incident/Elements/Tabs, 
    Ticket => $TicketObj, 
    current_tab => 'RTIR/Display.html?id='.$id,
    current_subtab => 'RTIR/Display.html?id='.$id,
    Title => loc("Incident #[_1]: [_2]", $id, $TicketObj->Subject) &>
    
% $m->callback(CallbackName => 'BeforeActionList', %ARGS, Actions => \@results, ARGSRef => \%ARGS, Ticket => $TicketObj);
<& /Elements/ListActions, actions => \@results &>

<table class="ticket-summary" width="100%"><tr><td class="boxcontainer" valign="top">

<&| /Widgets/TitleBox,
    title => loc('Incident #[_1]', $id), 
    title_href => RT->Config->Get('WebPath') ."/RTIR/Edit.html?id=".$id, 
    class=> 'ticket-info-basics',
&>

<table>
  <tr>
    <td class="label"><&|/l&>Owner</&>:</td>
    <td class="value"><& /Elements/ShowUser, User => $TicketObj->OwnerObj &></td>
  </tr>
  <tr>
    <td class="label"><&|/l&>Subject</&>:</td>
    <td class="value"><% $TicketObj->Subject %></td>
  </tr>
  <tr>
    <td class="label"><&|/l&>Priority</&>:</td>
    <td class="value"><& /Ticket/Elements/ShowPriority, Ticket => $TicketObj &></td>
  </tr>
  <tr>
    <td class="label"><&|/l&>Time Worked</&>:</td>
    <td class="value"><%loc('[_1] min', $TimeWorked)%></td>
  </tr>
</table>

<& /Elements/ShowCustomFields, Object => $TicketObj &>

</&>

</td><td class="boxcontainer" valign="top">

<& /RTIR/Incident/Elements/ShowChildren,
    IncidentObj => $TicketObj, 
    Queue       => 'Incident Reports',
&>

</td></tr><tr><td class="boxcontainer" valign="top">

<& /RTIR/Incident/Elements/ShowChildren,
    IncidentObj => $TicketObj, 
    Queue       => 'Investigations',
&>

</td><td class="boxcontainer" valign="top">

<& /RTIR/Incident/Elements/ShowChildren,
    IncidentObj => $TicketObj, 
    Queue       => 'Blocks',
&>

</td></tr><tr><td class="boxcontainer" valign="top">

<&| /Widgets/TitleBox,
    title => loc("Dates"),
    title_href => RT->Config->Get('WebPath') ."/RTIR/Edit.html?id=".$id, 
    class => 'ticket-info-dates',
&>
<& /RTIR/Elements/ShowDates, Ticket => $TicketObj &>
</&>

<br />

<& /Ticket/Elements/ShowAttachments, Ticket => $TicketObj, Attachments => $attachments &>
<& /Ticket/Elements/ShowRequestor, Ticket => $TicketObj, DisplayPath => "/RTIR/Display.html" &>

</td><td class="boxcontainer" valign="top">

% if ($RT::FM::VERSION) {
<&| /Widgets/TitleBox,
    title => loc("Articles"),
    title_href => RT->Config->Get('WebPath') ."/RTIR/Advanced.html?id=".$id, 
    class=> 'ticket-info-articles',
&>

% if ($TicketObj->CurrentUserHasRight('ModifyTicket')) {
% my $qs = $m->comp("/Elements/QueryString", "RefersTo-new" => "t:$id", next => RT->Config->Get('WebPath')."/RTIR/Display.html?id=$id");
    <table width="100%"><tr><td align="right"><font size="-1">
        | <a href="<%RT->Config->Get('WebPath')%>/RTFM/Article/PreCreate.html?<% $qs |n%>"><%loc('New')%></a> 
        | <a href="<%RT->Config->Get('WebPath')%>/RTIR/Incident/LinkArticles.html?id=<%$id%>"><%loc("Link")%></a> |
    </font></td></tr></table>
% }

<ul>
% while (my $Link = $TicketObj->RefersTo->Next) {
% next unless UNIVERSAL::isa($Link->TargetURI->Object, "RT::FM::Article");
<li><a href="<%$Link->TargetURI->Resolver->HREF%>"><% $Link->TargetURI->Object->Name %></a></li>
% }
</ul>

</&>
% }
</td></tr></table>

<& /Ticket/Elements/ShowHistory,
    Ticket         => $TicketObj,
    Collapsed      => $ARGS{'Collapsed'},
    ShowHeaders    => $ARGS{'ShowHeaders'},
    DisplayPath    => RT->Config->Get('WebPath') ."/RTIR/Display.html?id=$id",
    UpdatePath     => RT->Config->Get('WebPath') ."/RTIR/Update.html",
    ForwardPath    => RT->Config->Get('WebPath') ."/RTIR/Forward.html",
    EncryptionPath => RT->Config->Get('WebPath') ."/RTIR/GnuPG.html",
&>
<%INIT>

if ($SelectedTicket) {
    $id = $SelectedTicket;
    $ARGS{'Status'} = "open";
}
Abort('No incident specified') unless $id;

if( $Child ) {
    my $ChildObj = RT::Ticket->new( $session{'CurrentUser'} );
    $ChildObj->Load($Child);
    $m->callback(CallbackName => 'ThisLinkedToIR', ARGSRef => \%ARGS, Child => $ChildObj);
}


my $take_or_steal = sub {
    my $owner = $_[0]->Owner;
    return () if $owner == $session{'CurrentUser'}->id;
    my $action = 'Steal';
    $action = 'Take' if $owner == $RT::Nobody->id;
    my ($res, $msg) = $_[0]->$action();
    return $msg || ();
};

my $deleted_links = 0;
my $DoLinks = sub {
    my ($child, $parent) = @_;

    if ( RT->Config->Get('_RTIR_Constituency_Propagation') eq 'reject' ) {
        return loc("Couldn't link tickets with different constituencies")
            unless lc $child->FirstCustomFieldValue('Constituency') 
                   eq lc $parent->FirstCustomFieldValue('Constituency');
    }

    my %args = ();

    # Blocks or Incedent Reports can have multiple incidents
    my $Type = RT::IR::TicketType( Ticket => $child );
    unless( $Type eq 'Block' || $Type eq 'Report' ) {
        my $query = "Queue = 'Incidents' AND HasMember = ". $child->Id ." AND id != ". $parent->id;
        my $incidents = RT::Tickets->new( $session{'CurrentUser'} );
        $incidents->FromSQL( $query );
        while ( my $incident = $incidents->Next ) {
            $deleted_links = 1;
            $args{'DeleteLink-'. $child->id .'-MemberOf-'. $incident->id } = '';
        }
    }

    my @results;

    # if we don't own any of ends, take or steal parent
    unless ( $parent->Owner == $session{'CurrentUser'}->id
             || $child->Owner == $session{'CurrentUser'}->id )
    {
        push @results, $take_or_steal->( $parent );
    }

    # turn off strict check localy
    my $strict_acl = RT->Config->Set( StrictLinkACL => 0 );
    $args{ $child->Id .'-MemberOf' } = $parent->Id;
    push @results, ProcessTicketLinks( TicketObj => $child, ARGSRef => \%args );
    RT->Config->Set( StrictLinkACL => $strict_acl );

    return map { loc("Ticket [_1]: [_2]", $child->Id, $_) } @results;
};

my @results;

my $TicketObj;
my $new_ticket = 0;
if ( $id eq 'new' ) {
    my $QueueObj = RT::Queue->new( $session{'CurrentUser'} );
    $QueueObj->Load( $Queue ) || Abort(loc("Couldn't load queue."));
    unless( $QueueObj->Name eq 'Incidents' ) {
        return $m->comp('/RTIR/Display.html', %ARGS );
    }
    ($TicketObj, @results) = CreateTicket( %ARGS, Attachments => delete $session{'Attachments'} );
    $new_ticket = 1;
} else {
    $TicketObj = LoadTicket( $id );
}

$m->callback(CallbackName => 'ProcessArguments', 
            Ticket => $TicketObj,
            ARGSRef => \%ARGS,  
            Actions => \@results);

unless( $TicketObj->QueueObj->Name eq 'Incidents' ) {
    return $m->comp('/RTIR/Display.html', %ARGS );
}
$ARGS{'id'} = $id = $TicketObj->Id;

if( 1 ) {
    my $oldstate = RT::IR::Ticket::FirstCustomFieldValue( $TicketObj, 'State' );

    if ( $ARGS{'Action'} && $ARGS{'Action'} =~ /^(Steal|Kill|Take|SetTold)$/ )
    {   
        my $action = $1;
        my ($res, $msg) = $TicketObj->$action();
        push @results, $msg;
        # If they succeeded in taking the ticket, they should also get a lock
        if ($res && $action =~  /^(?:Take|Steal)$/) { $ARGS{'Lock'} = 'add'; }

    }



    unless( $new_ticket ) {
        push @results, ProcessTicketBasics( ARGSRef => \%ARGS, TicketObj => $TicketObj );
    }

    if ( $ARGS{'BulkLink'} || $Child ) {
        my @link;
        push @link, @SelectedTickets if $ARGS{'BulkLink'};
        push @link, $Child if $Child;
        foreach my $id ( grep $_, @link ) {
            my $child = RT::Ticket->new( $session{'CurrentUser'} );
            $child->Load( $id );
            unless( $child->id ) {
                push @results, loc("Couldn't load ticket #[_1] for linking.", $id);
                next;
            }
            push @results, $DoLinks->( $child, $TicketObj );
        }
    }
    if ( $ARGS{'BulkArticles'} && @SelectedTickets ) {
        my $ref = join " ", map "a:$_", @SelectedTickets;
        push @results, ProcessTicketLinks( TicketObj => $TicketObj, ARGSRef => {$TicketObj->Id."-RefersTo" => $ref} );
    }

    unless( $new_ticket ) {
        $ARGS{UpdateAttachments} = delete $session{'Attachments'};
        push @results, ProcessUpdateMessage( ARGSRef => \%ARGS, TicketObj => $TicketObj );
    }

    my $newstate = RT::IR::Ticket::FirstCustomFieldValue( $TicketObj, 'State' );
    if ($newstate ne $oldstate) {
        push @results, loc("State changed from [_1] to [_2]", $oldstate, $newstate );
    }
}

# XXX: need the way to show @results when aborting
unless ( $TicketObj->CurrentUserHasRight('ShowTicket') ) {
    Abort("No permission to view ticket #$id.");
}

my $TimeWorked = $TicketObj->TimeWorked;
if ( $TicketObj->TimeLeft > 0 ) {
    $TimeWorked .= '/'. $TicketObj->TimeLeft;
}

# If we deleted any links, check if what we deleted from still has children
if ( $deleted_links ) {
    # XXX: Can we ever get here?
    my $children = new RT::Tickets( $session{'CurrentUser'} );
    $children->LimitQueue( VALUE => 'Incident Reports' );
    $children->LimitQueue( VALUE => 'Investigations' );
    $children->LimitQueue( VALUE => 'Blocks' );
    $children->LimitMemberOf( $id );
    unless ( $children->Count ) {
        push @results, loc("WARNING: Incident [_1] has no children.", $id);
    }
}

my $attachments = $m->comp('/Ticket/Elements/FindAttachments', Ticket => $TicketObj);

</%INIT>

<%ARGS>
$id => undef
$Queue => 'Incidents'
$Child => undef
$SelectedTicket => undef
@SelectedTickets => ()
</%ARGS>
